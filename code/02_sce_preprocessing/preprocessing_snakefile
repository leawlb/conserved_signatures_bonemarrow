#!/bin/python 

#-------------------------------------------------------------------------------

import pandas as pd

# paths from config
METADATA_PATH = config["metadata"]["raw"]
OUTPUT_BASE_PATH = config["paths"]["output_dir"]

# objects from config
IDENTIFIER = config["metadata"]["identifier"]
METADATA = pd.read_csv(config["metadata"]["raw"])
VALUES =  config["metadata"]["values"]

# get all possible wildcards from designated identifier
targets = METADATA[IDENTIFIER]
targets = targets.drop_duplicates()
targets = targets.squeeze()
targets = targets.tolist()

# construct paths for all possible outputs, this is required for rule all
targets_02 = [OUTPUT_BASE_PATH + "/drop/" + "sce_" + x + "-02" for x in targets]
targets_03 = [OUTPUT_BASE_PATH + "/outl/" + "sce_" + x + "-03" for x in targets]
targets_04 = [OUTPUT_BASE_PATH + "/norm/" + "sce_" + x + "-04" for x in targets]
targets_05 = [OUTPUT_BASE_PATH + "/dimr/" + "sce_" + x + "-05" for x in targets]

targets_rp = ["reports/preprocessing_report_"+ x + ".html" for x in targets]
target_srp = ["reports/summary_report.html"]

if config["run_report"]: # only run summary_report if necessary
  targets_rp = targets_rp + target_srp

# local execution of non-demanding rules
localrules: remove_outliers, all  

# define rules
rule all: # must contain all possible output paths
    input:
        targets_02,
        targets_03,
        targets_04,
        targets_05,
        targets_rp

# remove empty droplets and doublets to retain only single cells
rule remove_droplets:
    input: 
        sce_01 = config["paths"]["input_dir"] + "{Object_ID}"
    output:
        sce_02 = OUTPUT_BASE_PATH + "/drop/sce_{Object_ID}-02"
    params:
        cutoff_umis = VALUES["cutoff_umis"],
        cutoff_doublets = VALUES["cutoff_doublets"]
    script:
        "scripts/02_remove_droplets.R"
        
# find outlier cells with low quality and remove them
rule remove_outliers:
    input: 
        sce_02 = rules.remove_droplets.output
    output:
        sce_03 = OUTPUT_BASE_PATH + "/outl/sce_{Object_ID}-03"
    params:
        cutoff_sum = VALUES["cutoff_sum"],
        cutoff_detected = VALUES["cutoff_detected"]
    script:
        "scripts/03_remove_outliers.R"
        
# normalize expression on sample level
rule normalize_expr:
    input: 
        sce_03 = rules.remove_outliers.output
    output:
        sce_04 = OUTPUT_BASE_PATH + "/norm/sce_{Object_ID}-04"
    script:
        "scripts/04_normalize_expr.R"
        
# extract hvgs and reduce dimensions for QC on sample level 
rule reduce_dims:
    input: 
        sce_04 = rules.normalize_expr.output
    output:
        sce_05 = OUTPUT_BASE_PATH + "/dimr/sce_{Object_ID}-05"
    params:
        nr_hvgs = VALUES["nr_hvgs"]
    script:
        "scripts/05_reduce_dims.R"

# construct report files to monitor QC on sample level
rule make_report:
    input:
        sce_01 = config["paths"]["input_dir"] + "{Object_ID}",
        sce_02 = rules.remove_droplets.output,
        sce_03 = rules.remove_outliers.output,
        sce_04 = rules.normalize_expr.output,
        sce_05 = rules.reduce_dims.output
    output:
        "reports/preprocessing_report_{Object_ID}.html"
    params:
        cutoff_umis = VALUES["cutoff_umis"],
        cutoff_doublets = VALUES["cutoff_doublets"],
        cutoff_sum = VALUES["cutoff_sum"],
        cutoff_detected = VALUES["cutoff_detected"],
        nr_hvgs = VALUES["nr_hvgs"]
    script:
        "make_report.Rmd" # .Rmd files should be stored in the snakemake wd
        
# make one summary report on all files
# because this rule is dependend on previous output but this is not reflected
# in input, it can be turned off in config to avoid premature job submission
print(config["run_report"])
if config["run_report"]:
  rule make_summary_report:
      input:
          #sce_01_path = config["paths"]["input_dir"],
          sce_02_path = OUTPUT_BASE_PATH + "/drop",
          sce_03_path = OUTPUT_BASE_PATH + "/outl",
          sce_04_path = OUTPUT_BASE_PATH + "/norm",
          sce_05_path = OUTPUT_BASE_PATH + "/dimr"
      output:
          "reports/summary_report.html"
      params:
          cutoff_umis = VALUES["cutoff_umis"],
          cutoff_doublets = VALUES["cutoff_doublets"],
          cutoff_sum = VALUES["cutoff_sum"],
          cutoff_detected = VALUES["cutoff_detected"],
          nr_hvgs = VALUES["nr_hvgs"],
          targets = targets
      script:
          "summary_report.Rmd" # .Rmd files should be stored in the snakemake wd
